grammar xklaim.Xklaim with org.eclipse.xtext.xbase.annotations.XbaseWithAnnotations

import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase

generate xklaim "http://www.Xklaim.xklaim"

XklaimModel:
	importSection=XImportSection?
	('package' name=QualifiedName)?
	processes+=XklaimProcess*
	nodes+=XklaimNode*
	nets+=XklaimNet*;

XklaimNode:
	'node' name=ID
	body=XBlockExpression
;

XklaimNet:
	'net' name=ID
	'physical' physicalLocality=STRING '{'
		nodes+=XklaimNetNode*
	'}'
;

XklaimNetNode:
	'node' name=ID
	('logical' logicalLocality=STRING)?
	body=XBlockExpression
;

XklaimAbstractNode: XklaimNode | XklaimNetNode;

XklaimProcess:
	'proc' name=ValidID
	'(' (params+=FullJvmFormalParameter (',' params+=FullJvmFormalParameter)*)? ')' (':' type=JvmTypeReference)? 
	body=XBlockExpression
;

// redefine Xbase primary expression with our special expressions
@Override 
XPrimaryExpression returns xbase::XExpression:
	XklaimAbstractOperation |
	XklaimInlineProcess |
	super
;

XklaimAbstractOperation returns xbase::XExpression:
	XklaimOutOperation | XklaimInOperation | XklaimNonBlockingInOperation
;

XklaimOutOperation returns XklaimAbstractOperation:
	{XklaimOutOperation} op='out' '(' arguments+=XExpression (',' arguments+=XExpression)* ')' '@' locality=XFeatureCall
;

XklaimInOperation returns XklaimAbstractOperation:
	{XklaimInOperation} op='in' '(' arguments+=XExpressionOrVarDeclaration (',' arguments+=XExpressionOrVarDeclaration)* ')' '@' locality=XFeatureCall
;

XklaimNonBlockingInOperation returns XklaimAbstractOperation:
	{XklaimNonBlockingInOperation} op='in_nb' '(' arguments+=XExpressionOrVarDeclaration (',' arguments+=XExpressionOrVarDeclaration)* ')' '@' locality=XFeatureCall
;

XklaimInlineProcess returns xbase::XExpression:
	{XklaimInlineProcess} 'proc' body=XExpression
;
