grammar xklaim.Xklaim with org.eclipse.xtext.xbase.annotations.XbaseWithAnnotations

import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase

generate xklaim "http://www.Xklaim.xklaim"

XklaimModel:
	('package' name=QualifiedName)?
	importSection=XImportSection?
	processes+=XklaimProcess*
	nodes+=XklaimNode*
	nets+=XklaimNet*;

XklaimNode:
	'node' name=ID
	('physical' physicalLocality=STRING)?
	(environment=XklaimNodeEnvironment)?
	body=XBlockExpression
;

XklaimNet:
	'net' name=ID
	'physical' physicalLocality=STRING '{'
		nodes+=XklaimNetNode*
	'}'
;

XklaimNetNode:
	'node' name=ID
	('logical' logicalLocality=STRING)?
	(environment=XklaimNodeEnvironment)?
	body=XBlockExpression
;

XklaimNodeEnvironment returns xbase::XBlockExpression:
	{XklaimNodeEnvironment} '['
		(expressions+=XklaimNodeEnvironmentEntry (',' expressions+=XklaimNodeEnvironmentEntry)*)?
	']'
;

XklaimNodeEnvironmentEntry returns xbase::XExpression:
	{XklaimNodeEnvironmentEntry} key=ValidID '->' value=XExpression
;

XklaimAbstractNode: XklaimNode | XklaimNetNode;

XklaimProcess:
	'proc' name=ValidID
	'(' (params+=FullJvmFormalParameter (',' params+=FullJvmFormalParameter)*)? ')' (':' type=JvmTypeReference)? 
	body=XBlockExpression
;

// redefine Xbase primary expression with our special expressions
@Override 
XPrimaryExpression returns xbase::XExpression:
	XklaimAbstractOperation |
	XklaimInlineProcess |
	super
;

XklaimAbstractOperation returns xbase::XExpression:
	XklaimOutOperation |
	XklaimInOperation | XklaimNonBlockingInOperation |
	XklaimReadOperation | XklaimNonBlockingReadOperation |
	XklaimEvalOperation
;

XklaimOutOperation returns XklaimAbstractOperation:
	{XklaimOutOperation} op='out' '(' arguments+=XExpression (',' arguments+=XExpression)* ')' '@' locality=XFeatureCall
;

XklaimInOperation returns XklaimAbstractOperation:
	{XklaimInOperation} op='in' '(' arguments+=XExpressionOrVarDeclaration (',' arguments+=XExpressionOrVarDeclaration)* ')'
	'@' locality=XFeatureCall
	('within' timeout=XExpression)?
;

XklaimNonBlockingInOperation returns XklaimAbstractOperation:
	{XklaimNonBlockingInOperation} op='in_nb' '(' arguments+=XExpressionOrVarDeclaration (',' arguments+=XExpressionOrVarDeclaration)* ')' '@' locality=XFeatureCall
;

XklaimReadOperation returns XklaimAbstractOperation:
	{XklaimReadOperation} op='read' '(' arguments+=XExpressionOrVarDeclaration (',' arguments+=XExpressionOrVarDeclaration)* ')'
	'@' locality=XFeatureCall
	('within' timeout=XExpression)?
;

XklaimNonBlockingReadOperation returns XklaimAbstractOperation:
	{XklaimNonBlockingReadOperation} op='read_nb' '(' arguments+=XExpressionOrVarDeclaration (',' arguments+=XExpressionOrVarDeclaration)* ')' '@' locality=XFeatureCall
;

XklaimEvalOperation returns XklaimAbstractOperation:
	{XklaimEvalOperation} op='eval' '(' arguments+=XExpression (',' arguments+=XExpression)* ')' '@' locality=XFeatureCall
;

XklaimInlineProcess returns xbase::XExpression:
	{XklaimInlineProcess} 'proc' body=XExpression
;
